<!DOCTYPE html>
<html>

<head>
    <title>Geoserver Config</title>
    <style type="text/css">
    * {
        box-sizing: border-box;
    }

    body {
        margin: 0;
        padding: 0;
        font-family: sans-serif;
    }

    canvas#gl-display {
        position: fixed;
        top: 0;
        width: 100vw;
        height: 100vh;
        z-index: -1;
    }

    div#links {
        position: absolute;
        left: 50%;
        height: 100%;
        padding-bottom: 2em;
        padding-top: 2em;
    }

    div.container {
        min-width: 300px;
        max-width: 730px;
        min-height: 100%;
        border-radius: 20px;
        background: rgba(255,255,255,0.7);
        position: relative;
        left: -50%;
        padding: 1em 2em;
    }

    </style>
</head>

<body>
    <canvas id="gl-display"></canvas>
    <div id="links">
        <div class="container">
            <h1>Geoserver 配置</h1>
        </div>
    </div>
</body>
<script id="vs" type="v-shader">
    attribute vec4 position;

    void main(){
    	gl_Position = position;
    }
</script>
<script id='fs' type="f-shader">
// lifted from glslsandbox.com
precision mediump float;

uniform float time;
uniform vec2 mouse;
uniform vec2 resolution;

float hash(vec3 p)
{
    p  = fract( p*0.3183099+.1 ); p *= 17.0;
    return fract( p.x*p.y*p.z*(p.x+p.y+p.z) );
}

float noise( in vec3 x )
{
    vec3 p = floor(x);
    vec3 f = fract(x);
    f = f*f*(3.0-2.0*f);

    return mix(mix(mix( hash(p+vec3(0,0,0)),
                        hash(p+vec3(1,0,0)),f.x),
                   mix( hash(p+vec3(0,1,0)),
                        hash(p+vec3(1,1,0)),f.x),f.y),
               mix(mix( hash(p+vec3(0,0,1)),
                        hash(p+vec3(1,0,1)),f.x),
                   mix( hash(p+vec3(0,1,1)),
                        hash(p+vec3(1,1,1)),f.x),f.y),f.z);
}

float map5( in vec3 p )
{
    vec3 q = p - vec3(0.0,0.1,1.0)*time;
    float f;
    f  = 0.50000*noise( q ); q = q*2.02;
    f += 0.25000*noise( q ); q = q*2.03;
    f += 0.12500*noise( q ); q = q*2.01;
    f += 0.06250*noise( q ); q = q*2.02;
    f += 0.03125*noise( q );
    return clamp( 1.5 - p.y - 2.0 + 1.75*f, 0.0, 1.0 );
}

float map4( in vec3 p )
{
    vec3 q = p - vec3(0.0,0.1,1.0)* time;
    float f;
    f  = 0.50000*noise( q ); q = q*2.02;
    f += 0.25000*noise( q ); q = q*2.03;
    f += 0.12500*noise( q ); q = q*2.01;
    f += 0.06250*noise( q );
    return clamp( 1.5 - p.y - 2.0 + 1.75*f, 0.0, 1.0 );
}

float map3( in vec3 p )
{
    vec3 q = p - vec3(0.0,0.1,1.0) * time;
    float f;
    f  = 0.50000*noise( q ); q = q*2.02;
    f += 0.25000*noise( q ); q = q*2.03;
    f += 0.12500*noise( q );
    return clamp( 1.5 - p.y - 2.0 + 1.75*f, 0.0, 1.0 );
}

float map2( in vec3 p )
{
    vec3 q = p - vec3(0.0,0.1,1.0)*time;
    float f;
    f  = 0.50000*noise( q ); q = q*2.02;
    f += 0.25000*noise( q );;
    return clamp( 1.5 - p.y - 2.0 + 1.75*f, 0.0, 1.0 );
}

vec3 sundir = normalize( vec3(-1.0,0.0,-1.0) );

vec4 integrate( in vec4 sum, in float dif, in float den, in vec3 bgcol, in float t )
{
    // lighting
    vec3 lin = vec3(0.65,0.7,0.75)*1.4 + vec3(1.0, 0.6, 0.3)*dif;
    vec4 col = vec4( mix( vec3(1.0,0.95,0.8), vec3(0.25,0.3,0.35), den ), den );
    col.xyz *= lin;
    col.xyz = mix( col.xyz, bgcol, 1.0-exp(-0.003*t*t) );
    // front to back blending
    col.a *= 0.4;
    col.rgb *= col.a;
    return sum + col*(1.0-sum.a);
}

#define MARCH(STEPS,MAPLOD) for(int i=0; i<STEPS; i++) { vec3  pos = ro + t*rd; if( pos.y<-3.0 || pos.y>2.0 || sum.a > 0.99 ) break; float den = MAPLOD( pos ); if( den>0.01 ) { float dif =  clamp((den - MAPLOD(pos+0.3*sundir))/0.6, 0.0, 1.0 ); sum = integrate( sum, dif, den, bgcol, t ); } t += max(0.05,0.02*t); }

vec4 raymarch( in vec3 ro, in vec3 rd, in vec3 bgcol, in ivec2 px )
{
    vec4 sum = vec4(0.0);
    float t = 0.0;//0.05*texelFetch( iChannel0, px&255, 0 ).x;

    MARCH(30,map5);
    MARCH(30,map4);
    MARCH(30,map3);
    MARCH(30,map2);

    return clamp( sum, 0.0, 1.0 );
}

mat3 setCamera( in vec3 ro, in vec3 ta, float cr )
{
    vec3 cw = normalize(ta-ro);
    vec3 cp = vec3(sin(cr), cos(cr),0.0);
    vec3 cu = normalize( cross(cw,cp) );
    vec3 cv = normalize( cross(cu,cw) );
    return mat3( cu, cv, cw );
}

vec4 render( in vec3 ro, in vec3 rd, in ivec2 px )
{
    // background sky
    float sun = clamp( dot(sundir,rd), 0.0, 1.0 );
    vec3 col = vec3(0.6,0.71,0.75) - rd.y*0.2*vec3(1.0,0.5,1.0) + 0.15*0.5;
    col += 0.2*vec3(1.0,.6,0.1)*pow( sun, 8.0 );

    // clouds
    vec4 res = raymarch( ro, rd, col, px );
    col = col*(1.0-res.w) + res.xyz;

    // sun glare
    col += 0.2*vec3(1.0,0.4,0.2)*pow( sun, 3.0 );

    return vec4( col, 1.0 );
}

void main()
{
    vec2 p = (-resolution.xy + 2.0*gl_FragCoord.xy)/ resolution.y;
    // camera
    vec3 ro = 3.0*normalize(vec3(0.0, 0.5, 2.0));
    vec3 ta = vec3(0.0, -1.0, 0.0);
    mat3 ca = setCamera( ro, ta, 0.0 );
    // ray
    vec3 rd = ca * normalize( vec3(p.xy, 1.0));

    gl_FragColor = render( ro, rd, ivec2(gl_FragCoord) );
}
</script>

<script src="./thirdparty/twgl_3_5_0/twgl.js"></script>

<script>
	"use strict";
    var precision = 6;
	var gl = twgl.getWebGLContext(document.getElementById("gl-display"));
	var programInfo = twgl.createProgramInfo(gl, ["vs", "fs"]);

	var arrays = {
	    position: [-1, -1, 0, 1, -1, 0, -1, 1, 0, -1, 1, 0, 1, -1, 0, 1, 1, 0],
	};
	var bufferInfo = twgl.createBufferInfoFromArrays(gl, arrays);

	function render(time) {
	    twgl.resizeCanvasToDisplaySize(gl.canvas);
        gl.canvas.width = gl.canvas.width / precision;
        gl.canvas.height = gl.canvas.height / precision;
	    gl.viewport(0, 0, gl.canvas.width, gl.canvas.height);

	    var uniforms = {
	        time: time * 0.001,
	        resolution: [gl.canvas.width, gl.canvas.height],
            mouse: [90, 120]
	    };

	    gl.useProgram(programInfo.program);
	    twgl.setBuffersAndAttributes(gl, programInfo, bufferInfo);
	    twgl.setUniforms(programInfo, uniforms);
	    twgl.drawBufferInfo(gl, bufferInfo);

	    requestAnimationFrame(render);
	}
	requestAnimationFrame(render);
</script>

</html>